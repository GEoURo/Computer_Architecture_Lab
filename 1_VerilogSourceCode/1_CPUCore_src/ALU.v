`timescale 1ns / 1ps
//////////////////////////////////////////////////////////////////////////////////
// Company: USTC ESLAB (Embeded System Lab)
// Engineer: Haojun Xia
// Create Date: 2019/02/08
// Design Name: RISCV-Pipline CPU
// Module Name: ALU
// Target Devices: Nexys4
// Tool Versions: Vivado 2017.4.1
// Description: ALU unit of RISCV CPU
//////////////////////////////////////////////////////////////////////////////////
`include "Parameters.v"   
module ALU(
    input wire [31:0] Operand1,
    input wire [31:0] Operand2,
    input wire [3:0] AluContrl,
    output reg [31:0] AluOut
    );

    always@(*)
    begin
      case(AluContrl)
        `SLL : AluOut = Operand1 << Operand2[4:0];
        `SRL : AluOut = Operand1 >> Operand2[4:0];
        `SRA : AluOut = $signed(Operand1) >>> Operand2[4:0];
        `ADD : AluOut = Operand1 + Operand2;
        `SUB : AluOut = Operand1 - Operand2;
        `XOR : AluOut = Operand1 ^ Operand2;
        `OR  : AluOut = Operand1 | Operand2;
        `AND : AluOut = Operand1 & Operand2;
        `SLT : AluOut = $signed(Operand1) < $signed(Operand2) ? 32'b01 : 32'b0;
        `SLTU: AluOut = $unsigned(Operand1) < $unsigned(Operand2) ? 32'b01 : 32'b0;
        `LUI : AluOut = Operand2;
        default : AluOut = 32'b0; 
      endcase
    end
endmodule

//åŠŸèƒ½å’ŒæŽ¥å£è¯´ï¿??
	//ALUæŽ¥å—ä¸¤ä¸ªæ“ä½œæ•°ï¼Œæ ¹æ®AluContrlçš„ä¸åŒï¼Œè¿›è¡Œä¸åŒçš„è®¡ç®—æ“ä½œï¼Œå°†è®¡ç®—ç»“æžœè¾“å‡ºåˆ°AluOut
	//AluContrlçš„ç±»åž‹å®šä¹‰åœ¨Parameters.vï¿??
//æŽ¨èæ ¼å¼ï¿??
    //case()
    //    `ADD:        AluOut<=Operand1 + Operand2; 
    //   	.......
    //    default:    AluOut <= 32'hxxxxxxxx;                          
    //endcase
//å®žéªŒè¦æ±‚  
    //å®žçŽ°ALUæ¨¡å—